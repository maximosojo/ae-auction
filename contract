payable contract Auction =
  // Offer
  record offer = 
    { address   : address,
      amount    : int }

  // Property
  record property =
    { url            : string,
      title          : string,
      description    : string,
      latitude       : string, 
      longitude      : string, 
      limit          : int,
      minOffer       : int,
      available      : bool,
      offers     : map(address, offer) }

  record state = 
    { properties       : map(int, property),
      propertiesLength : int }

  // Init
  entrypoint init() = { properties = {}, propertiesLength = 0 }

  // Get Property
  entrypoint getProperty(propertyId: int) : property = 
    state.properties[propertyId]

  // Get Properties
  entrypoint getProperties() = 
    state.properties

  // Properties Length
  entrypoint getPropertiesLength() = state.propertiesLength

  // Create Property
  stateful entrypoint createProperty(url': string, title': string, description': string,
                                    latitude': string, longitude': string,
                                    limit': int, minOffer': int) =
    // Parse values
    let property = 
      { url = url',
        title = title',
        description = description', 
        latitude = latitude', 
        longitude = longitude', 
        limit = limit',
        minOffer = minOffer',
        available = true,
        offers = {} }
        
    let propertyId = getPropertiesLength()
    let newLength = propertyId + 1
    
    // save meeting in blockchain
    put(state{ properties[propertyId] = property, propertiesLength = newLength })

  // Auction Property
  payable stateful entrypoint auctionProperty(propertyId: int) =
    let property = getProperty(propertyId)
  
    // Validate available
    if (!property.available) abort("Property auction is finished!")
    
    // Validate min amount offer
    if (Call.value < property.minOffer) abort("The offer is less than the minimum!")
    
    let updatedLimit = property.limit - 1

    let offers = property.offers

    let offer =
      { address = Call.caller,
        amount = Call.value }
    
    let updatedOffers = offers{ [Call.caller] = offer }
    
    let updatedOffer = property{ offers = updatedOffers, limit = updatedLimit }
    
    let updatedOffers = state.properties{ [propertyId] = updatedOffer }
    
    // update state
    put(state{ properties = updatedOffers })